// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.9.24'
    ext.aws_version = '2.75.2'
    ext.amplify_version = '2.19.1'
    ext.dokka_version = '1.9.20'
    ext.coroutine_version = '1.8.1'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.owasp:dependency-check-gradle:8.4.0'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
            classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"
        }
        classpath "gradle.plugin.com.cookpad.android.plugin:plugin:1.2.8"
        classpath "org.jmailen.gradle:kotlinter-gradle:3.16.0"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:4.0.0.2929"
        classpath ("com.squareup.moshi:moshi") {
            version{
                strictly '1.8.0'
            }
        }

    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "com.github.ben-manes.versions" version "0.39.0"
    id "com.apollographql.apollo3" version "4.0.0-beta.7"
}

ext {
    projectGroup = 'com.sudoplatform'
}

if(project.file("${rootProject.projectDir}/util/internal-repo.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/internal-repo.gradle"
}
else {
    ext["internalRepoUsername"] = ""
    ext["internalRepoPassword"] = ""
    ext["internalSnapshotRepoUrl"] = ""
    ext["internalSnapshotRepoName"] = ""
    ext["internalReleaseRepoUrl"] = ""
    ext["internalReleaseRepoName"] = ""
    ext["sonatypeStagingProfileId"] = ""
}

if (project.findProperty("tag") && sonatypeStagingProfileId) {
    nexusPublishing {
        repositories {
            sonatype {
                stagingProfileId = sonatypeStagingProfileId

                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        if (internalRepoUsername?.trim()) {
            maven {
                name = internalSnapshotRepoName
                url = internalSnapshotRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
            maven {
                name = internalReleaseRepoName
                url = internalReleaseRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Check outdated dependencies with suppressions
if (project.file("${rootProject.projectDir}/util/outdated-with-suppressions.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/outdated-with-suppressions.gradle"
}