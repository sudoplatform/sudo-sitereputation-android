// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.6.20'
    ext.coroutine_version = '1.5.2-native-mt'
    ext.dokka_version = '1.5.0'
    ext.aws_version = '2.52.1'
    ext.appsync_version = '3.3.2'

    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.owasp:dependency-check-gradle:6.5.3'
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
        if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
            classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"
        }
        classpath "com.cookpad.android.licensetools:license-tools-plugin:1.7.0"
        classpath "org.jmailen.gradle:kotlinter-gradle:3.6.0"
        classpath "com.amazonaws:aws-android-sdk-appsync-gradle-plugin:$appsync_version"
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id "com.github.ben-manes.versions" version "0.39.0"
}

ext {
    projectGroup = 'com.sudoplatform'
}

if(project.file("${rootProject.projectDir}/util/internal-repo.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/internal-repo.gradle"
}
else {
    ext["internalRepoUsername"] = ""
    ext["internalRepoPassword"] = ""
    ext["internalSnapshotRepoUrl"] = ""
    ext["internalSnapshotRepoName"] = ""
    ext["internalReleaseRepoUrl"] = ""
    ext["internalReleaseRepoName"] = ""
    ext["sonatypeStagingProfileId"] = ""
}

if (project.findProperty("tag") && sonatypeStagingProfileId) {
    nexusPublishing {
        repositories {
            sonatype {
                stagingProfileId = sonatypeStagingProfileId

                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
        if (internalRepoUsername?.trim()) {
            maven {
                name = internalSnapshotRepoName
                url = internalSnapshotRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
            maven {
                name = internalReleaseRepoName
                url = internalReleaseRepoUrl
                credentials {
                    username = internalRepoUsername
                    password = internalRepoPassword
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

// Check outdated dependencies with suppressions
if (project.file("${rootProject.projectDir}/util/outdated-with-suppressions.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/outdated-with-suppressions.gradle"
}